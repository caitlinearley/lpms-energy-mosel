model EPower
  uses "mmxprs"

declarations
  Emiss: set of string
	Source: set of string
  Times: set of real
  SeasonName: set of string
  PeriodLength: array(Times) of real
  Demand: array(Times) of real
	MaxOutput: array(Source) of real
  RunningCost:array(Source) of real
  IncreaseCost: array(Source) of real
  Emissions: array(Emiss,Source) of real
  EmissionsLimit: array(Emiss) of real
  SolarOutput: array(SeasonName, Times) of real
  ElectricityPrice: integer
  MaxHydroReserve: integer
  NaturalHydroInflow: integer
  HydroPowerEfficiencyFrac:	real
  AutumnMultiplier: real


  PowerOutput:  array(Source, Times) of mpvar
  IncreaseCostVar: array(Source, Times) of mpvar
  IncreasedPowerOutput: array(Source, Times) of mpvar
  RunningCostVar: array(Source, Times) of mpvar
  SolarOutputVar: array(Times) of mpvar
  Reserve: array(Times) of mpvar
  Pumping: array(Times) of mpvar


end-declarations
!Initialize decision variables

initializations from "EPower.dat"
	Emiss Source Times PeriodLength Demand MaxOutput RunningCost IncreaseCost SeasonName SolarOutput
	Emissions EmissionsLimit ElectricityPrice MaxHydroReserve NaturalHydroInflow HydroPowerEfficiencyFrac AutumnMultiplier
end-initializations

!Since wind energy is variable, we assume that, on average, the maximum wind output is 6000MW
!We then add a constraint to make sure power output does not exceed maximum power output for any source

WindAverage := MaxOutput("Wind")*0.5

forall(s in Source, t in Times) do
  MaxPowerOutputPerPeriod(s, t) := MaxOutput(s)
  MaxPowerOutputPerPeriod("Wind", t) := WindAverage
  OutputCS(s,t) := PowerOutput(s,t) <= MaxPowerOutputPerPeriod(s, t)

  !Defining the running cost for each source every period
  RunningCostVar(s, t) = RunningCost(s) * PowerOutput(s, t)
end-do

!Making sure that there is enough power output every period to satisfy both consumer demand and power needed to pump water into the reservoir
forall(t in Times) DemandCS(t):=sum(s in Source) PowerOutput(s,t) =( Demand(t) + Pumping(t)/PeriodLength(t) )


!Constraining the energy output from hydro every period to not exceed the potential energy held in the reserve
!Setting the reserve amount at the start of every period, taking into account the reserve at the beginning of the previous period, natural hydro inflow,
!and the amount pumped into the reserve (modified by an efficiency factor)
numtimes := getsize(Times)

forall(t in Times) do
  if(t=1) then
    HydroEnergyCS(t) := PowerOutput("Hydro", t)*PeriodLength(t) <= Reserve(t)
    Reserve(t) = (Reserve(numtimes) + NaturalHydroInflow * PeriodLength(numtimes) + HydroPowerEfficiencyFrac * Pumping(numtimes) - PowerOutput("Hydro", numtimes)*PeriodLength(numtimes))
  else
    HydroEnergyCS(t) := PowerOutput("Hydro", t)*PeriodLength(t)  <= Reserve(t)
    Reserve(t) = (Reserve(t-1) + NaturalHydroInflow * PeriodLength(t-1) + HydroPowerEfficiencyFrac * Pumping(t-1) - PowerOutput("Hydro", t-1)*PeriodLength(t-1))
  end-if
  !Constraining the potential energy in the reserve to not exceed the max hydro reserve
  MaxReserveCS(t) := Reserve(t) <= MaxHydroReserve
  MinReserveCS(t) := Reserve(t) >= 0
end-do


forall(t in Times) do
  OutputPerTime(t) := sum(s in Source) PowerOutput(s, t)
  PumpingCS(t) := Pumping(t) >=0
end-do


forall (s in Source, t in Times) do
  PowerOutputCS(s,t) := PowerOutput(s,t) >=0
end-do

!Setting the emission limits
forall (e in Emiss) do
  !TotalEmissions(e) := sum(s in Source, t in Times) (EnergyOutput(s,t) * Emissions(e, s))
  TotalEmissions(e) := sum(s in Source, t in Times) (PowerOutput(s,t) * PeriodLength(t)* Emissions(e, s))
  MaxTotalEmissionsCS(e) := TotalEmissions(e) <= EmissionsLimit(e)
  MinTotalEmissionsCS(e) := TotalEmissions(e) >= 0
end-do


!Defining the increased cost by first calculating the increase in power output per source per time period
forall(s in Source, t in Times) do
  MinIncreasedPowerOutputCS:= IncreasedPowerOutput(s,t) >= 0
  if (t=1) then
    IncreasedPowerOutput(s,t) >= (PowerOutput(s, t) - PowerOutput(s,numtimes))
  else
    IncreasedPowerOutput(s,t) >= (PowerOutput(s, t) - PowerOutput(s,t-1))
  end-if
  !IncreasedPowerOutput(s,t) := IncreasedEnergyOutput(s,t)/PeriodLength(t)
  IncreaseCostVar(s, t) = (IncreaseCost(s) ) * IncreasedPowerOutput(s,t)
end-do


RemoveCoal:= FALSE
RemoveGas:= FALSE
RemoveNuclear:= FALSE
WithSolar := FALSE
WithAutumn:= FALSE
forward procedure report

!Defining costs, revenues, and profits for stage 1
writeln;writeln("Stage 1: Without Solar and assuming 6000 MW of Wind Power")

forall(t in Times) PowerPerTime(t) :=sum(s in Source) (PowerOutput(s,t))

TotalCost := sum(s in Source, t in Times) (RunningCostVar(s, t) *PeriodLength(t) + IncreaseCostVar(s, t))
TotalRevenue := sum(t in Times) (PowerPerTime(t) *PeriodLength(t)-Pumping(t)) * ElectricityPrice
TotalProfit:= TotalRevenue - TotalCost
maximise(TotalProfit)
writeln("Total Profit = ", getobjval)
writeln("Total Revenue = ",getsol(TotalRevenue))
writeln("Total Costs = ",getsol(TotalCost))
forall(e in Emiss)writeln(strfmt(e,7), strfmt(getsol(TotalEmissions(e)),10))

report


forall(s in Source, t in Times) 

fopen("EPower.csv", F_OUTPUT)
writeln("Source, 1, 2, 3, 4, 5, 6, Total")
forall(s in Source) do
  write(s, ", ")
  forall(t in Times) write(getsol(PowerOutput(s, t)), ", ")
  write(sum(t in Times)getsol(PowerOutput(s, t)), ", ")
  writeln
end-do
write("Total, ")
forall(t in Times) do
  write(sum(s in Source) getsol(PowerOutput(s,t)), ", ")
end-do
write(getsol(sum(s in Source, t in Times)((PowerOutput(s,t)))))
fclose(F_OUTPUT)

!Calculating profit after reductions in CO2 emissioms
fopen("EPower_1.csv", F_OUTPUT)

write("PercentageChange, TotalProfit, Gas, Coal, Nuclear, Wind, Interconnect, Hydro, Sulphur, CO2")
writeln

Mu := 1.
PercentageChange := 0
repeat

TotalEmissionsCS("CO2") := TotalEmissions("CO2") <= Mu*EmissionsLimit("CO2")

maximise(TotalProfit)

write(PercentageChange, ", ", getobjval)
forall(s in Source) do
  write(", ", sum(t in Times)getsol(PowerOutput(s,t)))
end-do
forall(e in Emiss) write(", ",getsol(TotalEmissions(e)))
writeln

Mu -= 0.05
PercentageChange +=  5
until Mu < 0.5

fclose(F_OUTPUT)




!Calculating profit for stage 2
writeln;writeln("With Solar and assuming an Autumn Multiplier on Wind Power Output")


!Integrating a specific autumn multiplier on maximum wind output rather than taking a generic average
WithAutumn:= TRUE
forall(s in Source, t in Times) do
  MaxPowerOutputPerPeriod("Wind", t) := MaxOutput("Wind")  * AutumnMultiplier
  OutputCS(s,t) := PowerOutput(s,t) <= MaxPowerOutputPerPeriod(s, t)
end-do

!Adding solar power as a viable source of power output
WithSolar := TRUE

forall(t in Times) do
  SolarPerTimeCS(t) := SolarOutputVar(t) <= SolarOutput("Autumn",t)
  !DemandCS(t) := sum(s in Source)( PowerOutput(s,t) + SolarOutputVar(t) )= (Demand(t) +Pumping(t)/PeriodLength(t) )
  PowerPerTime(t):= sum(s in Source) PowerOutput(s,t)
  DemandCS(t) := (PowerPerTime(t) + SolarOutputVar(t) ) = (Demand(t) +Pumping(t)/PeriodLength(t) )
end-do


TotalCost := sum(s in Source, t in Times) (RunningCostVar(s, t)*PeriodLength(t) + IncreaseCostVar(s, t))
!TotalRevenue := sum(s in Source,t in Times) ((PowerOutput(s,t) + SolarOutputVar(t) )*PeriodLength(t)-Pumping(t) ) * ElectricityPrice
!TotalRevenue:= sum(s in Source) RevenuePerSource(s)
TotalRevenue := sum(t in Times) ((PowerPerTime(t)+ SolarOutputVar(t) )*PeriodLength(t)-Pumping(t) ) * ElectricityPrice
TotalProfit:= TotalRevenue - TotalCost
maximise(TotalProfit)

writeln("Total Profit = ", getobjval)
writeln("Total Revenue = ",getsol(TotalRevenue))
writeln("Total Costs = ",getsol(TotalCost))
forall(e in Emiss)writeln(strfmt(e,7), strfmt(getsol(TotalEmissions(e)),10))

report

fopen("EPower_2.csv", F_OUTPUT)
writeln("Source, 1, 2, 3, 4, 5, 6, Total")
forall(s in Source) do
  write(s, ", ")
  forall(t in Times) write(getsol(PowerOutput(s, t)), ", ")
  write(sum(t in Times)getsol(PowerOutput(s, t)))
  writeln
end-do
write("Solar",",")
forall(t in Times) write(getsol(SolarOutputVar(t)), ", ")
write(sum(t in Times)getsol(SolarOutputVar(t)))
writeln
write("Total, ")
forall(t in Times) do
  write(sum(s in Source) getsol(PowerOutput(s,t))+(getsol(SolarOutputVar(t))), ", ")
end-do
write(getsol(sum(s in Source, t in Times)((PowerOutput(s,t)))+sum(t in Times)(SolarOutputVar(t))))
fclose(F_OUTPUT)



writeln
writeln("Without Gas:")
writeln

RemoveGas := true
if RemoveGas then

  forall(s in Source, t in Times) do

    GasCS(s, t) := PowerOutput("Gas", t) = 0

  end-do
end-if
maximise(TotalProfit)
report
forall(s in Source, t in Times) GasCS(s, t) := PowerOutput("Gas", t) >= 0

writeln
writeln("Without Coal:")
writeln

RemoveCoal := true
if RemoveCoal then

  forall(s in Source, t in Times) do

    CoalCS(s, t) := PowerOutput("Coal", t) = 0

  end-do
end-if
maximise(TotalProfit)
report
forall(s in Source, t in Times) CoalCS(s, t) := PowerOutput("Coal", t) >= 0

writeln
writeln("Without Nuclear:")
writeln

RemoveNuclear := true
if RemoveNuclear then

  forall(s in Source, t in Times) do

    NuclearCS(s, t) := PowerOutput("Nuclear", t) = 0

  end-do
end-if
maximise(TotalProfit)
report
forall(s in Source, t in Times) NuclearCS(s, t) := PowerOutput("Nuclear", t) >= 0



writeln
! OLD CONSTRAINT CODE FOR REFERENCE

!forall(t in Times) DemandCS(t):=sum(s in Source) PowerOutput(s,t) = (Demand(t) + Pumping(t)/PeriodLength(t) )!

!forall(t in Times) do
 ! NewSolarPerTimeCS(t) := SolarOutputVar(t) <= 2 * SolarOutput("Autumn", t)
!end-do
!writeln("New demand constraint*****************************************************************************")

!forall(t in Times) NewDemandCS := sum(s in Source) PowerOutput(s,t) = 0.95*(Demand(t) + Pumping(t)/PeriodLength(t) )

!maximise(TotalProfit)

!report


procedure report


writeln;writeln("Power Output Per Source")
writeln( strfmt("Time Period",50))
write(strfmt("Source",15))
forall(t in Times) write(strfmt(t,15))
write(strfmt("Total",15))
writeln
forall(s in Source) do
  write(strfmt(s,15))
  forall(t in Times) write(strfmt(getsol(PowerOutput(s, t)),15))
  write(strfmt(sum(t in Times)getsol(PowerOutput(s, t)),15))
  writeln
end-do
write(strfmt("Pumping",15))
forall(t in Times) write(strfmt(getsol(Pumping(t)/PeriodLength(t)),15))
writeln
if (WithSolar)  then
  write(strfmt("Solar",15))
  forall(t in Times) write(strfmt(getsol(SolarOutputVar(t)),15))
  write(strfmt(sum(t in Times)getsol(SolarOutputVar(t)),15))
  writeln
end-if
write(strfmt("Total",15))
forall(t in Times) write(strfmt(getsol((sum(s in Source)(PowerOutput(s,t)))+SolarOutputVar(t)),15))
write(strfmt(getsol(sum(s in Source, t in Times)((PowerOutput(s,t)))+sum(t in Times)(SolarOutputVar(t))),15))
writeln

writeln;writeln("Energy Output Per Source")
writeln( strfmt("Time Period",50))
write(strfmt("Source",15))
forall(t in Times) write(strfmt(t,15))
write(strfmt("Total",15))
writeln
forall(s in Source) do
  write(strfmt(s,15))
  forall(t in Times) write(strfmt(getsol(PowerOutput(s, t))*PeriodLength(t),15))
  write(strfmt(sum(t in Times)getsol(PowerOutput(s, t)*PeriodLength(t)),15))
  writeln
end-do
write(strfmt("Pumping",15))
forall(t in Times) write(strfmt(getsol(Pumping(t)),15))
writeln
if (WithSolar)  then
  write(strfmt("Solar",15))
  forall(t in Times) write(strfmt(getsol(SolarOutputVar(t)*PeriodLength(t)),15))
  write(strfmt(sum(t in Times)getsol(SolarOutputVar(t)*PeriodLength(t)),15))
  writeln
end-if
write(strfmt("Total",15))
forall(t in Times) write(strfmt(sum(s in Source)getsol((PowerOutput(s,t))+SolarOutputVar(t))*PeriodLength(t),15))
write(strfmt(sum(s in Source, t in Times)getsol((PowerOutput(s,t))+SolarOutputVar(t))*PeriodLength(t),15))
writeln
end-procedure



end-model

! Need to add:
!